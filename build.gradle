buildscript {
	dependencies {
		classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.25'
	}
}

apply plugin: 'org.jetbrains.kotlin.jvm'

configurations {
	r8
}

dependencies {
	implementation 'com.squareup.okio:okio:2.9.0'
	implementation 'com.squareup.moshi:moshi:1.11.0'

	testImplementation 'junit:junit:4.13'
	testImplementation 'com.google.truth:truth:1.0.1'

	r8 'com.android.tools:r8:8.5.35'
}

def fatJarProvider = tasks.register('fatJar', Jar) { task ->
	task.dependsOn(configurations.named('runtimeClasspath'))
	task.dependsOn(tasks.named('jar'))

	task.archiveClassifier = 'fat'

	task.manifest {
		attributes 'Main-Class': 'com.jakewharton.asciinema.vsync.-Main'
	}

	def sourceClasses = sourceSets.main.output.classesDirs
	task.inputs.files(sourceClasses)

	task.doFirst {
		task.from files(sourceClasses)
		task.from configurations.runtimeClasspath.asFileTree.files.collect { zipTree(it) }
		task.exclude '**/*.kotlin_metadata'
		task.exclude '**/*.kotlin_module'
		task.exclude '**/*.kotlin_builtins'
		task.exclude '**/module-info.class'
		task.exclude 'META-INF/maven/**'
	}
}

def r8File = new File("$buildDir/libs/${base.archivesName.get()}-r8.jar")
def r8RulesFile = file('src/main/rules.txt')
def r8Jar = tasks.register('r8Jar', JavaExec) { task ->
	task.dependsOn(configurations.named('runtimeClasspath'))
	task.inputs.files(fatJarProvider.get().outputs.files)
	task.inputs.file(r8RulesFile)
	task.outputs.file(r8File)

	task.classpath(configurations.r8)
	task.mainClass = 'com.android.tools.r8.R8'
	task.args = [
		'--release',
		'--classfile',
		'--output', r8File.toString(),
		'--pg-conf', r8RulesFile.toString(),
		'--lib', System.properties['java.home'].toString()
	]
	doFirst {
		task.args += fatJarProvider.get().archiveFile.get()
	}
}

def binaryFile = new File("$buildDir/${base.archivesName.get()}.jar")
def binaryJar = tasks.register('binaryJar') { task ->
	task.dependsOn(r8Jar)

	task.inputs.file(r8File)
	task.outputs.file(binaryFile)

	task.doLast {
		binaryFile.getParentFile().mkdirs()
		binaryFile.delete()
		binaryFile << "#!/bin/sh\n\nexec java \$JAVA_OPTS -jar \$0 \"\$@\"\n\n"
		r8File.withInputStream { binaryFile.append it }

		binaryFile.setExecutable true, false
	}
}

tasks.named('assemble').configure { task ->
	task.dependsOn(binaryJar)
}

artifacts {
	archives file: binaryFile, name: 'binary', type: 'jar', builtBy: binaryJar, classifier: 'binary'
}

buildscript {
	repositories {
		mavenCentral()
	}
}
repositories {
	mavenCentral()
	google()
}
